S	            ::=   	(#x20 | #x9 | #xD | #xA)+
Eq	            ::=   	S? '=' S?
AttValue	      ::=   	'"' [^<&"]* '"'
NameStartChar	::=   	":" | [A-Z] | "_" | [a-z] | [#xC0-#xD6] | [#xD8-#xF6] | [#xF8-#x2FF] | [#x370-#x37D] | [#x37F-#x1FFF] | [#x200C-#x200D] | [#x2070-#x218F] | [#x2C00-#x2FEF] | [#x3001-#xD7FF] | [#xF900-#xFDCF] | [#xFDF0-#xFFFD] | [#x10000-#xEFFFF]
NameChar	      ::=   	NameStartChar | "-" | "." | [0-9] | #xB7 | [#x0300-#x036F] | [#x203F-#x2040]
Name	         ::=   	NameStartChar (NameChar)*
CharData	      ::=   	[^<&]*
Comment	      ::=   	'<!--' ((Char - '-') | ('-' (Char - '-')))* '-->'
element	      ::=   	EmptyElemTag | STag content ETag
STag	         ::=   	'<' Name (S Attribute)* S? '>'
Attribute	   ::=   	Name Eq AttValue
ETag	         ::=   	'</' Name S? '>'
content	      ::=   	S* CharData? ((element | EntityRef | Comment) S* CharData?)*
EmptyElemTag	::=   	'<' Name (S Attribute)* S? '/>'
EntityRef	   ::=   	'&' Name ';'